import org.gradle.api.attributes.java.TargetJvmVersion

plugins {
    id "project.defaults"
    id "base.maven_publish"
    id "publishing.aggregating_publisher"
}

configurations {
    include
}

dependencies {
    include(project(":core"))
    include(project(":j9")) {
        attributes {
            attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, 9)
        }
    }
    include(project(":j24")) {
        attributes {
            attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, 24)
        }
    }
}

evaluationDependsOn(":core")
evaluationDependsOn(":j9")
evaluationDependsOn(":j24")
jar {
    dependsOn(configurations.include)
    manifest {
        attributes "Multi-Release": "true"
    }

    dependsOn project(":core").tasks.named("classes")
    dependsOn project(":j9").tasks.named("classes")
    dependsOn project(":j24").tasks.named("classes")
    from({ project(":core").sourceSets.main.output }) {
    }
    from({ project(":j9").sourceSets.main.output }) {
        into "META-INF/versions/9"
    }
    from({ project(":j24").sourceSets.main.output }) {
        into "META-INF/versions/24"
    }
}
