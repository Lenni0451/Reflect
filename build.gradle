import org.gradle.api.attributes.java.TargetJvmVersion

plugins {
    id "project.defaults"
    id "base.maven_publish"
    id "publishing.aggregating_publisher"
    alias libs.plugins.jarTransformer
}

configurations {
    include
    includeJ9
    include.extendsFrom(includeJ9)
    includeJ24
    include.extendsFrom(includeJ24)
}

dependencies {
    include project(":core")
    includeJ9(project(":j9")) {
        attributes {
            attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, 9)
        }
    }
    includeJ24(project(":j24")) {
        attributes {
            attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, 24)
        }
    }
}

jar {
    manifest {
        attributes "Multi-Release": "true"
    }
    dependsOn(configurations.include)
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.include.collect {
            zipTree(it)
        }
    } {
        exclude("META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA")
    }
}

jarTransformer {
    dependency {
        configuration = configurations.includeJ9
        exclude {
            excludes = [
                    "META-INF"
            ]
        }
        repackage {
            relocations = [
                    "net.lenni0451": "META-INF.versions.9.net.lenni0451"
            ]
            remapClasses = true
            remapManifest = false
            removeEmptyDirs = true
            remapLog4jPlugins = false
        }
    }
    dependency {
        configuration = configurations.includeJ24
        exclude {
            excludes = [
                    "META-INF"
            ]
        }
        repackage {
            relocations = [
                    "net.lenni0451": "META-INF.versions.24.net.lenni0451"
            ]
            remapClasses = true
            remapManifest = false
            removeEmptyDirs = true
            remapLog4jPlugins = false
        }
    }
}
