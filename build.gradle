import org.gradle.api.attributes.java.TargetJvmVersion

plugins {
    id "project.defaults"
    id "base.maven_publish"
    id "publishing.aggregating_publisher"
}

def VERSION_SUBMODULES = [9, 11, 21, 24]

configurations {
    include
}

dependencies {
    include(project(":core"))
    VERSION_SUBMODULES.each { version ->
        include(project(":j$version")) {
            attributes {
                attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, version)
            }
        }
    }
}

evaluationDependsOn(":core")
VERSION_SUBMODULES.each { version ->
    evaluationDependsOn(":j$version")
}
jar {
    dependsOn(configurations.include)
    manifest {
        attributes "Multi-Release": "true"
    }

    dependsOn project(":core").tasks.named("classes")
    from({ project(":core").sourceSets.main.output }) {
    }
    VERSION_SUBMODULES.each { version ->
        dependsOn project(":j$version").tasks.named("classes")
        from({ project(":j$version").sourceSets.main.output }) {
            into "META-INF/versions/$version"
        }
    }
}
